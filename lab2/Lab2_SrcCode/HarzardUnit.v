`timescale 1ns / 1ps
//////////////////////////////////////////////////////////////////////////////////
// Company: USTC ESLAB 
// Engineer: Wu Yuzhang
// 
// Design Name: RISCV-Pipline CPU
// Module Name: HarzardUnit
// Target Devices: Nexys4
// Tool Versions: Vivado 2017.4.1
// Description: Deal with harzards in pipline
//////////////////////////////////////////////////////////////////////////////////
//åŠŸèƒ½è¯´æ˜
    //HarzardUnitç”¨æ¥å¤„ç†æµæ°´çº¿å†²çªï¼Œé€šè¿‡æ’å…¥æ°”æ³¡ï¼Œforwardä»¥åŠå†²åˆ·æµæ°´æ®µè§£å†³æ•°æ®ç›¸å…³å’Œæ§åˆ¶ç›¸å…³ï¼Œç»„åˆé?»è¾‘ç”µè·¯
    //å¯ä»¥æœ?åå®ç°ã?‚å‰æœŸæµ‹è¯•CPUæ­£ç¡®æ€§æ—¶ï¼Œå¯ä»¥åœ¨æ¯ä¸¤æ¡æŒ‡ä»¤é—´æ’å…¥å››æ¡ç©ºæŒ‡ä»¤ï¼Œç„¶åç›´æ¥æŠŠæœ¬æ¨¡å—è¾“å‡ºå®šä¸ºï¼Œä¸forwardï¼Œä¸stallï¼Œä¸flush 
//è¾“å…¥
    //CpuRst                                    å¤–éƒ¨ä¿¡å·ï¼Œç”¨æ¥åˆå§‹åŒ–CPUï¼Œå½“CpuRst==1æ—¶CPUå…¨å±€å¤ä½æ¸…é›¶ï¼ˆæ‰€æœ‰æ®µå¯„å­˜å™¨flushï¼‰ï¼ŒCpu_Rst==0æ—¶cpuå¼?å§‹æ‰§è¡ŒæŒ‡ä»?
    //ICacheMiss, DCacheMiss                    ä¸ºåç»­å®éªŒé¢„ç•™ä¿¡å·ï¼Œæš‚æ—¶å¯ä»¥æ— è§†ï¼Œç”¨æ¥å¤„ç†cache miss
    //BranchE, JalrE, JalD                      ç”¨æ¥å¤„ç†æ§åˆ¶ç›¸å…³
    //Rs1D, Rs2D, Rs1E, Rs2E, RdE, RdM, RdW     ç”¨æ¥å¤„ç†æ•°æ®ç›¸å…³ï¼Œåˆ†åˆ«è¡¨ç¤ºæºå¯„å­˜å™?1å·ç ï¼Œæºå¯„å­˜å™?2å·ç ï¼Œç›®æ ‡å¯„å­˜å™¨å·ç 
    //RegReadE RegReadD[1]==1                   è¡¨ç¤ºA1å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼ŒRegReadD[0]==1è¡¨ç¤ºA2å¯¹åº”çš„å¯„å­˜å™¨å€¼è¢«ä½¿ç”¨åˆ°äº†ï¼Œç”¨äºforwardçš„å¤„ç?
    //RegWriteM, RegWriteW                      ç”¨æ¥å¤„ç†æ•°æ®ç›¸å…³ï¼ŒRegWrite!=3'b0è¯´æ˜å¯¹ç›®æ ‡å¯„å­˜å™¨æœ‰å†™å…¥æ“ä½?
    //MemToRegE                                 è¡¨ç¤ºExæ®µå½“å‰æŒ‡ä»? ä»Data Memoryä¸­åŠ è½½æ•°æ®åˆ°å¯„å­˜å™¨ä¸­
//è¾“å‡º
    //StallF, FlushF, StallD, FlushD, StallE, FlushE, StallM, FlushM, StallW, FlushW    æ§åˆ¶äº”ä¸ªæ®µå¯„å­˜å™¨è¿›è¡Œstallï¼ˆç»´æŒçŠ¶æ€ä¸å˜ï¼‰å’Œflushï¼ˆæ¸…é›¶ï¼‰
    //Forward1E, Forward2E                                                              æ§åˆ¶forward
//å®éªŒè¦æ±‚  
    //è¡¥å…¨æ¨¡å—  
    
    
module HarzardUnit(
    input wire CpuRst, ICacheMiss, DCacheMiss, 
    input wire BranchE, JalrE, JalD, 
    input wire [4:0] Rs1D, Rs2D, Rs1E, Rs2E, RdE, RdM, RdW,
    input wire [1:0] RegReadE,
    input wire MemToRegE,
    input wire [2:0] RegWriteM, RegWriteW,
    output reg StallF, FlushF, StallD, FlushD, StallE, FlushE, StallM, FlushM, StallW, FlushW,
    output reg [1:0] Forward1E, Forward2E
    );
    
    // è¯·è¡¥å…¨æ­¤å¤„ä»£ç ?
    //äº§ç”Ÿforwardæ§åˆ¶ä¿¡å·
    always @(*)
    begin
        //gernerate Forward1E
        if(Rs1E != 0 && Rs1E == RdM && (|RegWriteM) != 0 && RegReadE[1] == 1)
        begin
            //mem to ex forward1
            Forward1E = 2'b10;
        end
        else if(Rs1E != 0 && Rs1E == RdW && (|RegWriteW) != 0 && RegReadE[1] == 1)
        begin
            //wb to ex forward1
            Forward1E = 2'b01;
        end
        else
        begin
            Forward1E = 2'b00;
        end
    end
    //gernerate Forward2E
    always @(*)
    begin   
        if(Rs2E != 0 && Rs2E == RdM && (|RegWriteM) != 0 && RegReadE[0] == 1) //dest reg ä¸èƒ½æ˜?0å·å¯„å­˜å™¨
        begin
            //mem to ex forward2
            Forward2E = 2'b10;
        end
        else if(Rs2E != 0 && Rs2E == RdW && (|RegWriteW) != 0 && RegReadE[0] == 1)
        begin
            //wb to ex forward2
            Forward2E = 2'b01;
        end
        else
        begin
            Forward2E = 2'b00;
        end
    end
    //gernerate Stall load-use
    //gernerate Flush branch(2), Jump(1)
    //gernerate StallF, FlushF, StallD, FlushD, StallE, FlushE
    always @(*)
    begin
        if(CpuRst)
        begin
            FlushF = 1'b1;
            FlushD = 1'b1;
            FlushE = 1'b1;
            StallF = 1'b0;
            StallD = 1'b0;
            StallE = 1'b0;
        end
        else
        begin
            //gernerate stall load-use,åœ¨IDæ®µå³è¿›è¡Œåˆ¤æ–­
            if((Rs1D == RdE  || Rs2D == RdE) && MemToRegE == 1 )
            begin
                //stall and bubble down
                StallF = 1'b1;
                StallD = 1'b1;
                FlushE = 1'b1;

                FlushF = 1'b0;
                FlushD = 1'b0;
                StallE = 1'b0;
            end
            //generate flush succeffully branch ,Jal, Jalr
            else if(BranchE == 1)
            begin
                FlushD = 1'b1;
                FlushE = 1'b1;
                
                FlushF = 1'b0;
                StallF = 1'b0;
                StallD = 1'b0;
                StallE = 1'b0;
            end
            else if(JalrE == 1)
            begin
                FlushD = 1'b1;
                FlushE = 1'b1;
                
                FlushF = 1'b0;
                StallF = 1'b0;
                StallD = 1'b0;
                StallE = 1'b0;
            end
            else if(JalD == 1)
            begin
                FlushD = 1'b1;

                FlushF = 1'b0;
                StallF = 1'b0;
                StallD = 1'b0;
                FlushE = 1'b0;
                StallE = 1'b0;
            end
            else
            begin
                FlushF = 1'b0;
                StallF = 1'b0;
                FlushD = 1'b0;
                StallD = 1'b0;
                FlushE = 1'b0;
                StallE = 1'b0;
            end
        end
    end
    //generate FlushM, StallM
    always @(*)
    begin
        if(CpuRst)
        begin
            FlushM = 1'b1;
            StallM = 1'b0;
        end
        else
        begin
            FlushM = 1'b0;
            StallM = 1'b0;
        end
    end
    //generate FlushW, StallW
    always @(*)
    begin
        if(CpuRst)
        begin
            FlushW = 1'b1;
            StallW = 1'b0;
        end
        else
        begin
            FlushW = 1'b0;
            StallW = 1'b0;
        end
    end

endmodule

  